--
--  01.15.00.SqlDataProvider
--
--  Author:
--       Roman M. Yagodin <roman.yagodin@gmail.com>
--
--  Copyright (c) 2017 Roman M. Yagodin
--
--  This program is free software: you can redistribute it and/or modify
--  it under the terms of the GNU Affero General Public License as published by
--  the Free Software Foundation, either version 3 of the License, or
--  (at your option) any later version.
--
--  This program is distributed in the hope that it will be useful,
--  but WITHOUT ANY WARRANTY; without even the implied warranty of
--  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
--  GNU Affero General Public License for more details.
--
--  You should have received a copy of the GNU Affero General Public License
--  along with this program.  If not, see <http://www.gnu.org/licenses/>.

-- NOTE: To manually execute this script you must 
-- replace {databaseOwner} and {objectQualifier} with real values. 
-- Defaults is "dbo." for database owner and "" for object qualifier 

IF EXISTS (select * from {databaseOwner}[{objectQualifier}ModuleDefinitions] where DefinitionName = N'R7.University.Division')
BEGIN
    -- Rename old definitions
    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_Division' WHERE DefinitionName = N'R7.University.Division'

    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_DivisionDirectory' WHERE DefinitionName = N'R7.University.DivisionDirectory'

    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_EduProgram' WHERE DefinitionName = N'R7.University.EduProgram'

    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_EduProgramDirectory' WHERE DefinitionName = N'R7.University.EduProgramDirectory'

    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_EduProgramProfileDirectory' WHERE DefinitionName = N'R7.University.EduProgramProfileDirectory'

    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_Employee' WHERE DefinitionName = N'R7.University.Employee'

    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_EmployeeDirectory' WHERE DefinitionName = N'R7.University.EmployeeDirectory'

    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_EmployeeList' WHERE DefinitionName = N'R7.University.EmployeeList'

    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_EmployeeDetails' WHERE DefinitionName = N'R7.University.EmployeeDetails'

    UPDATE {databaseOwner}[{objectQualifier}ModuleDefinitions]
        SET DefinitionName = N'R7_University_Launchpad' WHERE DefinitionName = N'R7.University.Launchpad'
END
GO

-- EduProgramDivisions

IF NOT EXISTS (select * from sys.tables where name = N'{objectQualifier}University_EduProgramDivisions')
BEGIN
    CREATE TABLE {databaseOwner}[{objectQualifier}University_EduProgramDivisions] (
        EduProgramDivisionID bigint IDENTITY (1,1) NOT NULL,
        DivisionID int NOT NULL,
        EduProgramID int NULL,
        EduProgramProfileID int NULL,
        DivisionRole nvarchar (255) NULL
        CONSTRAINT [PK_{objectQualifier}University_EduProgramDivisions] PRIMARY KEY (EduProgramDivisionID)
        CONSTRAINT [FK_{objectQualifier}University_EduProgramDivisions_Divisions] FOREIGN KEY (DivisionID)
            REFERENCES {databaseOwner}[{objectQualifier}University_Divisions] (DivisionID) ON DELETE CASCADE,
        CONSTRAINT [FK_{objectQualifier}University_EduProgramDivisions_EduPrograms] FOREIGN KEY (EduProgramID)
            REFERENCES {databaseOwner}[{objectQualifier}University_EduPrograms] (EduProgramID) ON DELETE SET NULL,
        CONSTRAINT [FK_{objectQualifier}University_EduProgramDivisions_EduProgramProfiles] FOREIGN KEY (EduProgramProfileID)
            REFERENCES {databaseOwner}[{objectQualifier}University_EduProgramProfiles] (EduProgramProfileID) ON DELETE SET NULL
    )

    INSERT INTO {databaseOwner}[{objectQualifier}University_EduProgramDivisions] (DivisionID, EduProgramID)
        SELECT EP.DivisionID, EP.EduProgramID FROM {databaseOwner}[{objectQualifier}University_EduPrograms] EP
            WHERE EP.DivisionID IS NOT NULL

    INSERT INTO {databaseOwner}[{objectQualifier}University_EduProgramDivisions] (DivisionID, EduProgramProfileID)
        SELECT EPP.DivisionID, EPP.EduProgramProfileID FROM {databaseOwner}[{objectQualifier}University_EduProgramProfiles] EPP
            WHERE EPP.DivisionID IS NOT NULL
END
GO

IF NOT EXISTS (select * from sys.indexes where name = N'IX_{objectQualifier}University_EduProgramDivisions')
BEGIN
    CREATE NONCLUSTERED INDEX [IX_{objectQualifier}University_EduProgramDivisions]
        ON {databaseOwner}[{objectQualifier}University_EduProgramDivisions] (DivisionID, EduProgramID, EduProgramProfileID)
END
GO

IF EXISTS (select * from sys.foreign_keys where name = N'FK_{objectQualifier}University_EduPrograms_Divisions')
BEGIN
    ALTER TABLE {databaseOwner}[{objectQualifier}University_EduPrograms]
        DROP CONSTRAINT [FK_{objectQualifier}University_EduPrograms_Divisions]
END
GO

IF EXISTS (select * from sys.foreign_keys where name = N'FK_{objectQualifier}University_EduProgramProfiles_Divisions')
BEGIN
    ALTER TABLE {databaseOwner}[{objectQualifier}University_EduProgramProfiles]
        DROP CONSTRAINT [FK_{objectQualifier}University_EduProgramProfiles_Divisions]
END
GO

IF EXISTS (select * from sys.columns where object_id = object_id (N'{databaseOwner}[{objectQualifier}University_EduPrograms]') and name = N'DivisionID')
BEGIN
    IF EXISTS (select * from sys.indexes where name = N'IX_{objectQualifier}University_EduPrograms_Divisions')
        DROP INDEX [IX_{objectQualifier}University_EduPrograms_Divisions] ON {databaseOwner}[{objectQualifier}University_EduPrograms]

    ALTER TABLE {databaseOwner}[{objectQualifier}University_EduPrograms]
        DROP COLUMN DivisionID
END
GO

IF EXISTS (select * from sys.columns where object_id = object_id (N'{databaseOwner}[{objectQualifier}University_EduProgramProfiles]') and name = N'DivisionID')
BEGIN
    IF EXISTS (select * from sys.indexes where name = N'IX_{objectQualifier}University_EduProgramProfiles_Divisions')
        DROP INDEX [IX_{objectQualifier}University_EduProgramProfiles_Divisions] ON {databaseOwner}[{objectQualifier}University_EduProgramProfiles]

    ALTER TABLE {databaseOwner}[{objectQualifier}University_EduProgramProfiles]
        DROP COLUMN DivisionID
END
GO

-- TODO: Move to the base library

IF EXISTS (select * from sys.procedures where name = N'{objectQualifier}r7_DropDefaultConstraint')
    DROP PROCEDURE {databaseOwner}[{objectQualifier}r7_DropDefaultConstraint]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}r7_DropDefaultConstraint]
    @tableName nvarchar (255),
    @columnName nvarchar (255)
AS
BEGIN
    DECLARE @sql nvarchar (max)
    SELECT TOP (1) @sql = N'ALTER TABLE {databaseOwner}[{objectQualifier}'+ @tableName +'] DROP CONSTRAINT [' + DC.name + N']'
        FROM sys.default_constraints DC
        JOIN sys.columns C ON C.default_object_id = DC.object_id
        WHERE DC.parent_object_id = OBJECT_ID (@tableName) AND C.name = @columnName
    IF @@ROWCOUNT = 1 EXECUTE sp_executesql @sql
END
GO

IF NOT EXISTS (select * from sys.columns where object_id = object_id(N'{databaseOwner}[{objectQualifier}University_EduProgramProfileForms]') and name = N'TimeToLearnHours')
BEGIN
    ALTER TABLE {databaseOwner}[{objectQualifier}University_EduProgramProfileForms]
        ADD TimeToLearnHours int NOT NULL DEFAULT (0)

    EXECUTE sp_executesql N'UPDATE {databaseOwner}[{objectQualifier}University_EduProgramProfileForms]
        SET TimeToLearnHours = TimeToLearn, TimeToLearn = 0
        WHERE TimeToLearnUnit = N''h'''

    EXECUTE {databaseOwner}[{objectQualifier}r7_DropDefaultConstraint] @tableName = N'University_EduProgramProfileForms', @columnName = N'TimeToLearnUnit'

    ALTER TABLE {databaseOwner}[{objectQualifier}University_EduProgramProfileForms]
        DROP COLUMN TimeToLearnUnit
END
GO